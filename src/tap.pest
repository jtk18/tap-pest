
newline    = _{ NEWLINE }
tap = { ( version? ~ preplan_nontest_line* ~ leading_plan ~ lines ) | postplan_line+ ~ trailing_plan? ~ nontest_line* }
version = { "TAP version " ~ positiveInteger ~ NEWLINE }
leading_plan = _{ plan ~ skip_directive? ~ NEWLINE }
trailing_plan = _{ plan ~ NEWLINE }
plan = { "1.." ~ positiveInteger }
lines = _{ line+ }
line = _{ ( comment | test | bailout | unknown ) ~ NEWLINE }
postplan_line = _{ (comment | test | bailout | postplan_unknown ) ~ NEWLINE }
test = { status ~ positiveInteger? ~ desc_text? ~ directive? }
status = { "not "? ~ "ok " }
directive = _{ todo_directive | skip_directive }
todo_directive = { hash ~ ^"TODO" ~ " " ~ text_output? }
skip_directive = { hash ~ ^"SKIP" ~ " " ~ text_output? }
nontest_line = _{ comment | unknown ~ NEWLINE }
preplan_nontest_line = _{ comment | preplan_unknown ~ NEWLINE }
comment = { hash ~ text_output? }
hash = _{ hash_char ~ " "? }
bailout = { "Bail out!" ~ text_output? }
unknown = { (!NEWLINE ~ ANY)+ }
preplan_unknown = { (!NEWLINE ~ !leading_plan ~ ANY)+ }
postplan_unknown = { (!NEWLINE ~ !trailing_plan ~ ANY)+ }
text_output = { (!NEWLINE ~ ANY)+ }
desc_text = { ( !NEWLINE ~ !hash_char ~ ANY)+ }
hash_char = _{ "#" }
positiveInteger = { ASCII_NONZERO_DIGIT ~ (ASCII_DIGIT+)? }

// https://github.com/Perl-Toolchain-Gang/Test-Harness/blob/94e8ba4c942a0f4e4eb0a483a8a8c3ee9bd9ff61/lib/TAP/Parser/Grammar.pm#L499
// tap            ::= version? { comment | unknown } leading_plan lines
//                    |
//                    lines trailing_plan {comment}
// version        ::= 'TAP version ' positiveInteger {positiveInteger} "\n"
// leading_plan   ::= plan skip_directive? "\n"
// trailing_plan  ::= plan "\n"
// plan           ::= '1..' nonNegativeInteger
// lines          ::= line {line}
// line           ::= (comment | test | unknown | bailout ) "\n"
// test           ::= status positiveInteger? description? directive?
// status         ::= 'not '? 'ok '
// description    ::= (character - (digit | '#')) {character - '#'}
// directive      ::= todo_directive | skip_directive
// todo_directive ::= hash_mark 'TODO' ' ' {character}
// skip_directive ::= hash_mark 'SKIP' ' ' {character}
// comment        ::= hash_mark {character}
// hash_mark      ::= '#' {' '}
// bailout        ::= 'Bail out!' {character}
// unknown        ::= { (character - "\n") }
//  (* POSIX character classes and other terminals *)
//  digit              ::= [:digit:]
//  character          ::= ([:print:] - "\n")
//  positiveInteger    ::= ( digit - '0' ) {digit}
//  nonNegativeInteger ::= digit {digit}